
sed ' {
    s/[ ]*$//
    s/^[ ]*//
    s/  */ /g
   /.*%.*/ {
    d
    }
1 s/.*/<html>\
<head>\
    <meta http-equiv="Content-Type" content="text\/html; charset=UTF-8" \/>\
    <title>&<\/title>\
<\/head>\
<body>/
    2,$ {
        /^\\section{[^}]*}$/!{
        /^\\subsection{[^}]*}$/!{
            /./ {
                H
                $ !{
                    d
                }
                $ {
                    x
                    s/.*/<p>&<\/p>/
                }
            }
        }}

        /^\\section{\([^}]*\)}$/{
            s/^\\section{\([^}]*\)}$/<h1>\1<\/h1>/
            x
            /./ {
                s/.*/<p>&<\/p>/
            }
            /^$/ {
                x
            }
        }

        /^\\subsection{[^}]*}$/ {
            s/^\\subsection{\([^}]*\)}$/<h2>\1<\/h2>/
            x
            /./ {
                s/.*/<p>&<\/p>/
            }
            /^$/ {
                x
            }
        }
        
        /^$/ {
            x
            /^$/d
            /^<h1>/!{
            /^<h2>/!{
                s/.*/<p>&<\/p>/
            }}
        }

        s/<p>\n/<p>/
    }
    $ a\
<\/body>\
<\/html>
}' 
